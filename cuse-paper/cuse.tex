\documentclass[lang=cn,11pt]{elegantpaper}

\title{CUSE：构建自己的 Stata 数据集仓库}
\author{\href{https://www.czxa.top}{程振兴}}
\version{2.0.0}
\date{2019年6月3日}

\usepackage[authoryear]{gbt7714}
\usepackage{url}
\usepackage{stata}

\begin{document}
\maketitle

\begin{abstract}
\noindent 几乎每个 Stata 的使用者都知道 \lstinline{sysuse}、\lstinline{webuse} 这些命令。它们能够非常方便的调用一些示例数据集。本文通过仿造 \cite{Mitchell2006A} 一书中提供的 \lstinline{vguse} 命令的结构构建了自己的 Stata 数据集仓库。使用数据集仓库可以非常方便的存放和调用自己的一些示例和常用数据集，我将自己的数据仓库命名为 \lstinline{cuse}。
\end{abstract}

\section{导论}

程序的使用演示经常需要一些示例数据集，存放在本地的示例数据集不便于分享，存放在服务器上的示例数据集虽然方便分享，但是使用的时候需要加上网址。例如我想介绍 Stata 的回归命令 \lstinline{regress} （\rref{regress}）。我使用数据集 \lstinline{grilic_small.dta} 进行演示，这个数据集的网址链接为：\url{https://github.com/czxa/cuse/raw/master/g/grilic.dta}。

\lstinline{regress} 的用法示例：

\begin{lstlisting}
  use https://github.com/czxa/cuse/raw/master/g/grilic.dta, clear
  regress lw s

  *>       Source |       SS           df       MS      Number of obs   =       758
  *> -------------+----------------------------------   F(1, 756)       =    255.70
  *>        Model |  35.2039946         1  35.2039946   Prob > F        =    0.0000
  *>     Residual |  104.082155       756  .137674809   R-squared       =    0.2527
  *> -------------+----------------------------------   Adj R-squared   =    0.2518
  *>        Total |   139.28615       757  .183997556   Root MSE        =    .37105
  *> ------------------------------------------------------------------------------
  *>           lw |      Coef.   Std. Err.      t    P>|t|     [95% Conf. Interval]
  *> -------------+----------------------------------------------------------------
  *>            s |   .0966245   .0060425    15.99   0.000     .0847624    .1084866
  *>        _cons |   4.391486   .0821136    53.48   0.000     4.230288    4.552684
  *> ------------------------------------------------------------------------------
\end{lstlisting}

而如果 \lstinline{grilic_small.dta} 是个系统数据集，就能使用 \lstinline{sysuse} 命令调用了，而 \lstinline{grilic_small.dta} 并不是一个系统数据集，所以使用  \lstinline{sysuse} 调用会失败：

\begin{lstlisting}
  sysuse grilic_small, clear
  *> file "grilic_small.dta" not found
  *> r(601);
\end{lstlisting}

这里就体现出构建一个自己的数据集仓库的必要性了。于是就有了 \lstinline{cuse} 命令。

\section{cuse 命令包的结构}

为了便于数据集的管理，我们首先需要建立名称分别为 \lstinline{a, b, c, ..., z} 和 \lstinline{0} 的文件夹，这些文件夹可以用来盛放不同字母开头的数据集。

然后我们需要编写一个 \lstinline{cuse} 命令，代码如下：

\begin{lstlisting}
  *! 功能1: 调用本地仓库数据集
  *! 功能2: 调用远端仓库数据集
  *! 功能3: 将数据集存入系统文件夹中
  cap prog drop cuse
  prog define cuse
  	version 14.0
  	if `"`0'"' == "" {
  		error 198
  	}
  	local 0 `"using `0'"'
  	syntax using/ [, Clear Web Savetosystem]
  	if "`web'" != ""{
  		local url "https://github.com/czxa/cuse/raw/master"
  	}
  	else{
  		local url "~/Documents/cuse"
  	}
  	local prefix = substr("`using'", 1, 1)
  	use `"`url'/`prefix'/`using'"', `clear'
  	if "`savetosystem'" != ""{
  		local syspath "`c(sysdir_plus)'"
  		save "`syspath'`prefix'/`using'", replace
  	}
  end
\end{lstlisting}

我为 \lstinline{cuse} 命令 设置了一个 \underline{w}eb 选项，该选项的功能是对数据集的位置进行判断，因为该仓库文件夹在我的电脑上存放的位置是 \lstinline{~/Documents/}，同时该仓库托管在 GitHub 上，我的 GitHub 主页的链接为：\url{https://github.com/czxa} 我自己平时使用数据集的时候可以直接从本地读取数据，这样会比较快，然后把代码分享给别人的时候在所有的 \lstinline{cuse} 语句后面添加 \underline{w}eb 选项。也就是说：

\begin{lstlisting}
  cuse grilic_small, clear
  * 等价于
  use ~/Documents/cuse/g/grilic_small, clear

  * 而
  cuse grilic_small, clear web
  * 等价于
  use https://github.com/czxa/cuse/raw/master/g/grilic_small, clear
\end{lstlisting}

除此之外，我还设置了 \underline{s}avetosystem 选项，使用该选项可以把数据集保存成系统数据集，这样就可以通过 \lstinline{sysuse} 命令使用了：

\begin{lstlisting}
  cuse grilic_small, clear web savetosystem
  *> (note: file /Users/czx/Library/Application Support/Stata/ado/plus/g/grilic_small.dta not found)
  *> file /Users/czx/Library/Application Support/Stata/ado/plus/g/grilic_small.dta saved

  sysuse grilic_small, clear
\end{lstlisting}

为了记录 cuse 仓库里到底有那些数据集，我又写了个 \lstinline{cuselist} 命令，这个命令通过下载一个临时的 \lstinline{cuselist_temp.ado} 命令执行，命令仓库的作者在仓库中添加新数据之后可以通过修改 \lstinline{cuselist_temp.ado} 文件进行记录。这样，使用者无需更新 \lstinline{cuse}  命令，每次运行 \lstinline{cuselist} 命令得到的数据集列表就是最新的。

\begin{lstlisting}
  *! 显示数据库中的所有数据集
  cap prog drop cuselist
  prog define cuselist
  	copy "https://github.com/czxa/cuse/raw/master/cuselist_temp.ado" cuselist_temp.ado, replace
  	cuselist_temp
  end
\end{lstlisting}

\lstinline{cuselist_temp.ado} 的代码目前为：

\begin{lstlisting}
  *! 显示数据库中的所有数据集
  cap prog drop cuselist_temp
  prog define cuselist_temp
  	di "【0】"
  	di "----------------------------------------------------------------------"
  	di "1. 000001.dta: 平安银行历史股票数据"
  	di "【a】"
  	di "----------------------------------------------------------------------"
  	di "1. amricancellmapdata.dta: 美国蜂窝地图各个省份的位置坐标"
  	di "【c】"
  	di "----------------------------------------------------------------------"
  	di "1. cellmapdata.dta: 中国蜂窝地图各个省份的位置坐标"
    ······（此处省略了一些代码）
  	di "2. tourism.dta: 旅游事业发展情况"
  	di "----------------------------------------------------------------------"
  	di "【书籍数据集】"
  	di "注意！如果你想调用的数据集的名字里含大写字母，你需要把它的首字母调成小写才能调用!"
  	di "1. 《计量经济学及Stata应用》——陈强著"
  	di "2. 《高级计量经济学及Stata应用》——陈强著"
  	di "3. 《An Introduction to Stata Programming, Second Edition》——Christopher F. Baum著"
  end
\end{lstlisting}

最后再添加一些辅助文件，就搭建好了一个数据集仓库。

\section{安装}

我把该命令存放在了 GitHub 上。Stata 提供了一种安装外部命令的基础命令：\lstinline{net install}，你可以在 Stata 的命令输出窗口输入下面的命令安装 cuse 命令：

\begin{lstlisting}
net install cuse, from("https://www.czxa.top/cuse")
\end{lstlisting}

另外推荐使用 \href{https://github.com/haghish}{E. F. Haghish} 开发的 \href{https://github.com/haghish/github}{github} 命令安装：

首先你需要安装github命令：

\begin{lstlisting}
net install github, from("https://haghish.github.io/github/")
\end{lstlisting}

然后就可以安装这个命令了：

\begin{lstlisting}
github install czxa/cuse, replace
\end{lstlisting}

\section{用法}

\begin{stsyntax}
  \centering
  cuse
  ["]filename["]\
  \optional{,
  \underbar{c}lear
  \underbar{w}eb
  \underbar{s}avetosystem
  }
\end{stsyntax}

filename：是你想要调用的数据集名称，例如上面的 \lstinline{grilic_small}。

\begin{enumerate}
  \item $\underline{c}lear$：可以简写为 c。使用该选项时会先清空已有数据集再读入。
  \item $\underline{w}eb$：可以简写 w。使用该选项时表示从 GitHub 上读取数据。
  \item $\underline{s}avetosystem$：可以简写 s。使用该选项时表示读入数据集后把该数据集存放在系统文件夹里。
\end{enumerate}

\section{用法示例}

从本地文件夹读取 \lstinline{ctbc2.dta} 数据集（只有自己能使用）：

\begin{lstlisting}
 cuse ctbc2, clear
 *> (2002年-2018年中债国债到期收益率)
\end{lstlisting}

从 GitHub 上读取 \lstinline{ctbc2.dta} 数据集：

\begin{lstlisting}
 cuse ctbc2, clear web
 *> (2002年-2018年中债国债到期收益率)
\end{lstlisting}

查看当前数据集仓库中的所有可用数据：

\begin{lstlisting}
  cuselist
  *> 【0】
  *> ----------------------------------------------------------------------
  *> 1. 000001.dta: 平安银行历史股票数据
  *> 【a】
  *> ----------------------------------------------------------------------
  *> 1. amricancellmapdata.dta: 美国蜂窝地图各个省份的位置坐标
  *> 【c】
  *> ----------------------------------------------------------------------
  *> 1. cellmapdata.dta: 中国蜂窝地图各个省份的位置坐标
  *> 1. countycode.dta: 中国各省市区县编号(即身份证前六位号码)
  ······（此处省略了一些代码结果）
  *> 【书籍数据集】
  *> 注意！如果你想调用的数据集的名字里含大写字母，你需要把它的首字母调成小写才能调用!
  *> 1. 《计量经济学及Stata应用》——陈强著
  *> 2. 《高级计量经济学及Stata应用》——陈强著
  *> 3. 《An Introduction to Stata Programming, Second Edition》——Christopher F. Baum著
\end{lstlisting}

\nocite{*}
\bibliography{cuse}

\end{document}
